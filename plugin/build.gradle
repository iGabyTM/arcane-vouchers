plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven { url 'https://jitpack.io' }

    maven {
        url 'https://repo.codemc.org/repository/maven-public/'
    }

    maven {
        name 'actions-core, actions-spigot'
        url 'https://repo.triumphteam.dev/snapshots'
    }

    maven {
        name 'TimeAPI'
        url 'https://repo.piggypiglet.me/releases'
    }

    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    // Actions
    compileOnly 'me.gabytm.util:actions-core:1.0.0-SNAPSHOT'
    implementation('me.gabytm.util:actions-spigot:1.0.0-SNAPSHOT') { exclude group: 'com.google.guava' }

    // Requirements
    implementation 'me.gabytm.minecraft.util:requirements-bukkit:1.0.0-SNAPSHOT'

    compileOnly('com.github.MilkBowl:VaultAPI:1.7') { transitive false }

    // NBT
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.11.3'

    // Adventure
    def adventureVersion = '4.14.0'
    implementation "net.kyori:adventure-api:$adventureVersion"
    implementation 'net.kyori:adventure-platform-bukkit:4.3.0'
    implementation "net.kyori:adventure-text-minimessage:$adventureVersion"

    // GUis and commands
    implementation 'dev.triumphteam:triumph-gui:3.1.5'
    implementation 'me.mattstudios.utils:matt-framework:1.4.6'

    // Heads
    compileOnly('com.arcaniax:HeadDatabase-API:1.3.1') { transitive false }

    // WorldGuard compatibility
    ['base', 'v6', 'v7'].forEach {implementation project(':plugin:compatibility:worldguard:' + it)  }

    // bStats
    implementation 'org.bstats:bstats-bukkit:3.0.0'

    // TimeAPI - https://github.com/PiggyPiglet/TimeAPI
    implementation 'me.piggypiglet:TimeAPI:+'

    // Other dependencies that don't have a repository or the release is old
    compileOnly fileTree(dir: './libs', includes: ['*.jar'])

    // PAPI
    compileOnly 'me.clip:placeholderapi:2.10.10'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

processResources {
    eachFile { expand version: project.version }
}

shadowJar {
    archiveName("ArcaneVouchers [${project.version}].jar")

    [
            'me.gabytm.util.actions': 'actions',
            'dev.triumphteam.gui'   : 'gui',
            'net.kyori.adventure'   : 'adventure',
            'me.mattstudios.mf'     : 'commands',
            'sh.okx.timeapi'        : 'timeapi',
            'org.bstats'            : 'bstats',
            'kotlin'                : 'kt'
    ].each { relocate(it.key, project.group + '.arcanevouchers.libs.' + it.value) }
}

tasks.register('copyToServer', Copy) {
    if (testServerVersion == '') {
        from shadowJar
        into '../server/plugins'
    } else {
        from shadowJar
        into '../server/.' + testServerVersion + '/plugins'
    }
}